import java.time.OffsetDateTime
import java.time.temporal.ChronoUnit

plugins {
    id 'com.github.ben-manes.versions' version '0.21.0'
    id 'org.kordamp.gradle.apidoc' version '0.22.0'
    id 'com.avast.gradle.docker-compose' version '0.9.4'
}

apply plugin: 'java'

description = 'Spring Boot Modular Project'

defaultTasks 'clean', 'build', 'install', 'publishToMavenLocal'

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
}

java {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
}

compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

def buildDate = OffsetDateTime.now().truncatedTo(ChronoUnit.SECONDS)

allprojects {
    group = 'ch.frostnova.spring-multimodule'
}
subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    repositories {
        jcenter()
    }
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    plugins.withType(JavaPlugin) {
        jar {
            manifest {
                attributes(
                        "Name": project.name,
                        "Version": project.version,
                        "Build-Date": buildDate
                )
            }
        }
    }
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    reportOn subprojects*.test
}
build.dependsOn testReport

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

ext {
    springBootVersion = '2.1.6.RELEASE'
    junitVersion = '4.12'
    mockitoVersion = '2.28.2'
    h2Version = '1.4.199'
    liquibaseVersion = '3.6.3'
    swaggerVersion = '2.9.2'
    jacksonVersion = '2.9.9'
    jerseyVersion = '2.28'
    javaValidationVersion = '2.0.1.Final'
	mysqlDriverVersion = '8.0.16'

    libs = [
            testbase          : [
                    "junit:junit:${junitVersion}",
                    "org.mockito:mockito-core:${mockitoVersion}"
            ],
            springBoot        : [
                    "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}",
                    "org.springframework.boot:spring-boot-starter-web:${springBootVersion}",
                    "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
            ],
            springBootTest    : [
                    "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
            ],
            springBootDevTools: [
                    "org.springframework.boot:spring-boot-devtools:${springBootVersion}"
            ],
            liquibase         : [
                    dependencies.create("org.liquibase:liquibase-core:${liquibaseVersion}") {
                        exclude module: 'snakeyaml' // 1.18 clashes with 1.23 from spring boot 2.1.5
                    }
            ],
            h2Database        : [
                    "com.h2database:h2:${h2Version}"
            ],
			mysql        : [
                    "mysql:mysql-connector-java:${mysqlDriverVersion}"
            ],
            swagger           : [
                    "io.springfox:springfox-swagger2:${swaggerVersion}",
                    "io.springfox:springfox-swagger-ui:${swaggerVersion}"
            ],
            json              : [
                    "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}",
                    "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}",
                    "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
            ],
            jaxrsClient       : [
                    "org.glassfish.jersey.core:jersey-client:${jerseyVersion}",
                    "org.glassfish.jersey.media:jersey-media-json-jackson:${jerseyVersion}",
                    "org.glassfish.jersey.inject:jersey-hk2:${jerseyVersion}"
            ],
            javaValidation    : "javax.validation:validation-api:${javaValidationVersion}"

    ]
}

task start(dependsOn: ['assemble', ':app:bootRun']) {
    group = 'Start/Run'
    description = 'Start the boot app'
}