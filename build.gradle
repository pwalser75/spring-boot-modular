import java.time.OffsetDateTime
import java.time.temporal.ChronoUnit

plugins {
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'com.avast.gradle.docker-compose' version '0.14.9'
    id 'net.ltgt.apt' version '0.21'
}

description = 'Spring Boot Modular Project'

defaultTasks 'clean', 'build', 'publishToMavenLocal'

allprojects {
    group = 'ch.frostnova.spring-multimodule'

    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
    }
}
subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    jar {
        manifest {
            attributes(
                    "Name": project.name,
                    "Version": project.version,
                    "Build-Date": OffsetDateTime.now().truncatedTo(ChronoUnit.SECONDS)
            )
        }
    }

    publishing {
        repositories {
            maven {
                url = "file://${project.rootDir}/repository/" + (version.endsWith('SNAPSHOT') ? 'snapshots' : 'releases')
            }
        }
    }


    if (!project.hasProperty('quick')) {
        task sourcesJar(type: Jar, dependsOn: classes) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        artifacts {
            archives sourcesJar
        }
    }

    clean {
        delete "./out"
        delete "./logs"
        delete "./db"
    }

    test {
        useJUnitPlatform()
    }

    javadoc.onlyIf { !project.hasProperty('quick') }
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/tests")
    reportOn subprojects*.test
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

ext {
    springBootVersion = '2.5.4'

    junitVersion = '5.8.0'
    assertjVersion = '3.20.2'
    mockitoVersion = '3.12.4'

    logstashLogbackVersion = '6.6'
    janinoVersion = '3.1.6'
    micrometerVersion = '1.7.3'

    hibernateVersion = '5.5.7.Final'
    liquibaseVersion = '4.4.3'
    h2Version = '1.4.200'
    mysqlDriverVersion = '8.0.26'

    swaggerVersion = '3.0.0'

    jacksonVersion = '2.12.5'
    jerseyVersion = '3.0.2'
    javaValidationVersion = '2.0.1.Final'

    jjwtVersion = '0.9.1'
    javaCacheVersion = '1.1.1'
    ehcacheVersion = '3.9.6'
    
    javaxAnnotationVersion = '1.3.2'

    libs = [
            api                   : [
                    "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion",
                    "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
                    "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion",
                    "javax.validation:validation-api:$javaValidationVersion",
                    "javax.annotation:javax.annotation-api:$javaxAnnotationVersion",
                    "io.springfox:springfox-swagger2:$swaggerVersion",
            ],
            testbase              : [
                    "org.junit.jupiter:junit-jupiter:$junitVersion",
                    "org.mockito:mockito-core:$mockitoVersion",
                    "org.assertj:assertj-core:$assertjVersion",
                    "org.glassfish.jersey.core:jersey-client:$jerseyVersion",
                    "org.glassfish.jersey.media:jersey-media-json-jackson:$jerseyVersion",
                    "org.glassfish.jersey.inject:jersey-hk2:$jerseyVersion"
            ],
            springBoot            : [
                    "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion",
                    "org.springframework.boot:spring-boot-starter-web:$springBootVersion",
                    "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion",
                    "org.springframework.boot:spring-boot-starter-security:$springBootVersion",
                    "org.springframework.boot:spring-boot-starter-cache:$springBootVersion",
                    "org.springframework.boot:spring-boot-starter-validation:$springBootVersion",
                    "net.logstash.logback:logstash-logback-encoder:$logstashLogbackVersion",
                    "org.codehaus.janino:janino:$janinoVersion",
                    "io.micrometer:micrometer-registry-prometheus:$micrometerVersion",
                    "io.springfox:springfox-swagger-ui:$swaggerVersion",
                    "mysql:mysql-connector-java:$mysqlDriverVersion",
                    "com.h2database:h2:$h2Version",
                    "javax.cache:cache-api:$javaCacheVersion",
                    "org.ehcache:ehcache:$ehcacheVersion",
                    "javax.annotation:javax.annotation-api:$javaxAnnotationVersion",

            ],
            springBootTest        : [
                    dependencies.create("org.springframework.boot:spring-boot-starter-test:$springBootVersion") {
                        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
                    }
            ],
            springBootDevTools    : [
                    "org.springframework.boot:spring-boot-devtools:$springBootVersion"
            ],
            liquibase             : [
                    dependencies.create("org.liquibase:liquibase-core:$liquibaseVersion") {
                        exclude module: 'snakeyaml' // 1.18 clashes with 1.23 from spring boot 2.1.5
                    }
            ],
            jpaAnnotationProcessor: [
                    "org.hibernate:hibernate-jpamodelgen:$hibernateVersion"
            ],
            jwt                   : [
                    "io.jsonwebtoken:jjwt:${jjwtVersion}"
            ]
    ]
}

task start(dependsOn: ['app:assemble', ':app:bootRun']) {
    group = 'Start/Run'
    description = 'Start the boot app'
}
